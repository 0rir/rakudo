use MONKEY-SEE-NO-EVAL;
use experimental :rakuast;
use Test;

plan 6;

my $ast;
my $deparsed;
sub ast(RakuAST::Node:D $node --> Nil) {
    $ast      := $node;
    $deparsed := $node.DEPARSE;
    diag $deparsed.chomp;
}

subtest 'Fat arrow syntax forms a Pair' => {
    # answer => 42
    ast RakuAST::FatArrow.new(
      key => 'answer',
      value => RakuAST::IntLiteral.new(42)
    );

    is-deeply $_, (answer => 42),
      for EVAL($ast), EVAL($deparsed);
}

subtest 'True colonpair forms a Pair with value True' => {
    # :r
    ast RakuAST::ColonPair::True.new(key => 'r');

    is-deeply $_, (r => True),
      for EVAL($ast), EVAL($deparsed);
}

subtest 'False colonpair forms a Pair with value False' => {
    # :!r
    ast RakuAST::ColonPair::False.new(key => 'r');

    is-deeply $_, (r => False),
      for EVAL($ast), EVAL($deparsed);
}

subtest 'Number colonpair forms a Pair with the correct Int value' => {
    # :answer(42)
    ast RakuAST::ColonPair::Number.new(
      key => 'answer',
      value => RakuAST::IntLiteral.new(42)
    );

    is-deeply $_, (answer => 42)
      for EVAL($ast), EVAL($deparsed);
}

subtest 'Value colonpair forms a Pair with the correct value' => {
    # :cheese<stilton>
    ast RakuAST::ColonPair::Value.new(
      key => 'cheese',
      value => RakuAST::StrLiteral.new('stilton')
    );

    is-deeply $_, (cheese => 'stilton')
      for EVAL($ast), EVAL($deparsed);
}

subtest 'Variable colonpair forms a Pair that looks up the variable' => {
    my $curry = 'red';

    # :$curry
    ast RakuAST::ColonPair::Variable.new(
      key => 'curry',
      value => RakuAST::Var::Lexical.new('$curry')
    );

    is-deeply $_, (curry => 'red')
      for EVAL($ast), EVAL($deparsed);
}

# vim: expandtab shiftwidth=4
