use v6.e.PREVIEW;
use Test;

plan 12;

my $ast;
my $deparsed;
my $raku;
my @type = <AST Str>;
sub ast(RakuAST::Node:D $node --> Nil) {
    $ast      := $node;
    $deparsed := $node.DEPARSE;
    $raku     := $node.raku;
    diag $deparsed.chomp;
}

subtest 'A pointy block node evaluates to a Block' => {
    # -> { 101 }
    ast RakuAST::Statement::Expression.new(
      expression => RakuAST::PointyBlock.new(
        body => RakuAST::Blockoid.new(
          RakuAST::StatementList.new(
            RakuAST::Statement::Expression.new(
              expression => RakuAST::IntLiteral.new(101)
            )
          )
        )
      )
    );
    is-deeply $deparsed, '-> { 101 }', 'deparse';

    for 'AST', EVAL($ast), 'Str', EVAL($deparsed) -> $type, $block {
        ok $block.WHAT === Block,
          "$type: A pointy block node evaluates to a Block";
        is $block.signature.params.elems, 0,
          "$type: The block has no parameters";
        is-deeply $block.arity, 0,
          "$type: The block has 0 arity";
        is-deeply $block.count, 0,
          "$type: The block has 0 count";
        is $block(), 101,
          "$type: Invoking the block returns the expected value";
        dies-ok { $block(1) },
          "$type: Invoking the block with an argument dies";
    }
}

subtest 'A pointy block node taking a parameter evaluates to a Block' => {
    # -> $param { $param }
    ast RakuAST::Statement::Expression.new(
      expression => RakuAST::PointyBlock.new(
        signature => RakuAST::Signature.new(
          parameters => (
            RakuAST::Parameter.new(
              target => RakuAST::ParameterTarget::Var.new('$param')
            ),
          )
        ),
        body => RakuAST::Blockoid.new(
          RakuAST::StatementList.new(
            RakuAST::Statement::Expression.new(
              expression => RakuAST::Var::Lexical.new('$param')
            )
          )
        )
      )
    );
    is-deeply $deparsed, '-> $param { $param }', 'deparse';

    for 'AST', EVAL($ast), 'Str', EVAL($deparsed) -> $type, $block {
        ok $block.WHAT === Block,
          "$type: A pointy block node taking a parameter evaluates to a Block";
        is $block.signature.params.elems, 1,
          "$type: The block has one parameters";
        is-deeply $block.arity, 1,
          "$type: The block has 1 arity";
        is-deeply $block.count, 1,
          "$type: The block has 1 count";
        is $block(199), 199,
          "$type: Invoking the block with an argument returns the expected value";
        dies-ok { $block(my $a = 42) = 1 },
          "$type: Argument is bound read-only";
        dies-ok { $block() },
          "$type: Invoking the block without an argument dies";
    }
}

subtest 'Bare block at statement level is executed' => {
    my $x = 99;

    # { $x++ };
    ast RakuAST::Statement::Expression.new(
      expression => RakuAST::Block.new(
        body => RakuAST::Blockoid.new(
          RakuAST::StatementList.new(
            RakuAST::Statement::Expression.new(
              expression => RakuAST::ApplyPostfix.new(
                operand => RakuAST::Var::Lexical.new('$x'),
                postfix => RakuAST::Postfix.new('++')
              )
            )
          )
        )
      )
    );
    is-deeply $deparsed, '{ $x++ }', 'deparse';

    is-deeply EVAL($ast), 99,
      'AST: Bare block at statement level is executed';
    is-deeply $x, 100,
      'AST: Side-effects were performed as expected';

    is-deeply EVAL($deparsed), 100,
      'Str: Bare block at statement level is executed';
    is-deeply $x, 101,
      'Str: Side-effects were performed as expected';
}

subtest 'Bare block in parentheses evaluates to Block' => {
    my $x = 99;

    # ({ $x++ })
    ast RakuAST::Statement::Expression.new(
      expression => RakuAST::Circumfix::Parentheses.new(
        RakuAST::SemiList.new(
          RakuAST::Statement::Expression.new(
            expression => RakuAST::Block.new(
              body => RakuAST::Blockoid.new(
                RakuAST::StatementList.new(
                  RakuAST::Statement::Expression.new(
                    expression => RakuAST::ApplyPostfix.new(
                      operand => RakuAST::Var::Lexical.new('$x'),
                      postfix => RakuAST::Postfix.new('++')
                    )
                  )
                )
              )
            )
          )
        )
      )
    );
    is-deeply $deparsed, '({ $x++ })', 'deparse';

    given EVAL($ast) -> $result {
        is-deeply $result.WHAT, Block,
          'AST: Bare block in parentheses evaluates to Block';
        is $result.arity, 0,
          'AST: Block has arity 0';
        is $result.count, 1,
          'AST: Block has count 1';
        is-deeply $x, 99,
          'AST: No side-effects were performed';
        is-deeply $result(), 99,
          'AST: Can evaluate the returned block';
        is-deeply $x, 100,
          'AST: Block did perform side-effects when evaluated';
    }

    given EVAL($deparsed) -> $result {
        is-deeply $result.WHAT, Block,
          'Str: Bare block in parentheses evaluates to Block';
        is $result.arity, 0,
          'Str: Block has arity 0';
        is $result.count, 1,
          'Str: Block has count 1';
        is-deeply $x, 100,
          'Str: No side-effects were performed';
        is-deeply $result(), 100,
          'Str: Can evaluate the returned block';
        is-deeply $x, 101,
          'Str: Block did perform side-effects when evaluated';
    }
}

subtest 'Block has default parameter' => {
    # ({ $_ })
    ast RakuAST::Statement::Expression.new(
      expression => RakuAST::Circumfix::Parentheses.new(
        RakuAST::SemiList.new(
          RakuAST::Statement::Expression.new(
            expression => RakuAST::Block.new(
              body => RakuAST::Blockoid.new(
                RakuAST::StatementList.new(
                  RakuAST::Statement::Expression.new(
                    expression => RakuAST::Var::Lexical.new('$_')
                  ),
                )
              )
            )
          )
        )
      )
    );
    is-deeply $deparsed, '({ $_ })', 'deparse';

    for 'AST', EVAL($ast), 'Str', EVAL($deparsed) -> $type, $result {
        is-deeply $result('xxx'), 'xxx',
          "$type: Block has default $type parameter";
        lives-ok { $result() },
          "$type: That $type parameter is optional";
    }
}

subtest 'A sub node evaluates to a Sub' => {
    # sub ($param) { $param }
    ast RakuAST::Statement::Expression.new(
      expression => RakuAST::Sub.new(
        signature => RakuAST::Signature.new(
          parameters => (
            RakuAST::Parameter.new(
              target => RakuAST::ParameterTarget::Var.new('$param')
            ),
          )
        ),
        body => RakuAST::Blockoid.new(
          RakuAST::StatementList.new(
            RakuAST::Statement::Expression.new(
              expression => RakuAST::Var::Lexical.new('$param')
            ),
          )
        )
      )
    );
    is-deeply $deparsed, 'sub ($param) { $param }', 'deparse';

    for 'AST', EVAL($ast), 'Str', EVAL($deparsed) -> $type, $sub {
        ok $sub.WHAT === Sub,
          "$type: A sub node evaluates to a Sub";
        is $sub.signature.params.elems, 1,
          "$type: The sub has one parameter";
        is-deeply $sub.arity, 1,
          "$type: The block has 1 arity";
        is-deeply $sub.count, 1,
          "$type: The block has 1 count";
        is $sub(189), 189,
          "$type: Invoking the sub with an argument returns the expected value";
        dies-ok { $sub() },
          "$type: Invoking the sub without an argument dies";
    }
}

subtest 'Can call a named sub declaration' => {
    # sub my-sub ($param) { $param }; my-sub(66)
    ast RakuAST::StatementList.new(
      RakuAST::Statement::Expression.new(
        expression => RakuAST::Sub.new(
          name => RakuAST::Name.from-identifier('my-sub'),
          signature => RakuAST::Signature.new(
            parameters => (
              RakuAST::Parameter.new(
                target => RakuAST::ParameterTarget::Var.new('$param')
              ),
            )
          ),
          body => RakuAST::Blockoid.new(
            RakuAST::StatementList.new(
              RakuAST::Statement::Expression.new(
                expression => RakuAST::Var::Lexical.new('$param')
              ),
            )
          )
        )
      ),
      RakuAST::Statement::Expression.new(
        expression => RakuAST::Call::Name.new(
          name => RakuAST::Name.from-identifier('my-sub'),
          args => RakuAST::ArgList.new(
            RakuAST::IntLiteral.new(66),
          )
        )
      )
    );
    is-deeply $deparsed, q:to/CODE/, 'deparse';
sub my-sub ($param) { $param }
my-sub(66)
CODE

    is-deeply $_, 66, @type[$++]
      for EVAL($ast), EVAL($deparsed);
}

subtest 'A routine declared anonymous does not declare anything' => {
    # anon sub my-sub () { 66 }; my-sub()
    ast RakuAST::StatementList.new(
      RakuAST::Statement::Expression.new(
        expression => RakuAST::Sub.new(
          scope => 'anon',
          name => RakuAST::Name.from-identifier('my-sub'),
          body => RakuAST::Blockoid.new(
            RakuAST::StatementList.new(
              RakuAST::Statement::Expression.new(
                expression => RakuAST::IntLiteral.new(66)
              ),
            )
          )
        )
      ),
      RakuAST::Statement::Expression.new(
        expression => RakuAST::Call::Name.new(
          name => RakuAST::Name.from-identifier('my-sub')
        )
      )
    );
    is-deeply $deparsed, q:to/CODE/, 'deparse';
anon sub my-sub () { 66 }
my-sub()
CODE

    dies-ok $_, @type[$++]
      for { EVAL($ast) }, { EVAL($deparsed) };
}

subtest 'A sub node with a trait evaluates to a Sub' => {
    # sub () returns Int { 66 }
    ast RakuAST::Statement::Expression.new(
      expression => RakuAST::Sub.new(
        traits => [
          RakuAST::Trait::Returns.new(
            RakuAST::Type::Simple.new(
              RakuAST::Name.from-identifier('Int')
            )
          )
        ],
        body => RakuAST::Blockoid.new(
          RakuAST::StatementList.new(
            RakuAST::Statement::Expression.new(
              expression => RakuAST::IntLiteral.new(66)
            )
          )
        )
      )
    );
    is-deeply $deparsed, 'sub () returns Int { 66 }', 'deparse';

    my $sub = EVAL($ast);
    ok $sub ~~ Sub, 'A sub node with a trait evaluates to a Sub';
    is-deeply $sub.returns, Int, 'The returns trait was applied and .returns is correct';
    ok $sub ~~ Callable[Int], 'It also does the correct parametric Callable';
}

subtest 'Return type constraint' => {
    my $x;

    # sub () returns Int { $x }
    ast RakuAST::Statement::Expression.new(
      expression => RakuAST::Sub.new(
        traits => [
          RakuAST::Trait::Returns.new(
            RakuAST::Type::Simple.new(
              RakuAST::Name.from-identifier('Int')
            )
          )
        ],
        body => RakuAST::Blockoid.new(
          RakuAST::StatementList.new(
            RakuAST::Statement::Expression.new(
              expression => RakuAST::Var::Lexical.new('$x')
            )
          )
        )
      )
    );
    is-deeply $deparsed, 'sub () returns Int { $x }', 'deparse';

    for 'AST', EVAL($ast), 'Str', EVAL($deparsed) -> $type, $sub {
        $x = 42;
        lives-ok { $sub() }, "$type: type matches";
        $x = 'oops';
        dies-ok { $sub() }, "$type: type does not match";
    }
}

subtest 'Using return with acceptable type works' => {
    my $x;

    # sub () returns Int { return $x }
    ast RakuAST::Statement::Expression.new(
      expression => RakuAST::Sub.new(
        traits => [
          RakuAST::Trait::Returns.new(
            RakuAST::Type::Simple.new(
              RakuAST::Name.from-identifier('Int')
            )
          )
        ],
        body => RakuAST::Blockoid.new(
          RakuAST::StatementList.new(
            RakuAST::Statement::Expression.new(
              expression => RakuAST::Call::Name.new(
                name => RakuAST::Name.from-identifier('return'),
                args => RakuAST::ArgList.new(
                  RakuAST::Var::Lexical.new('$x')
                )
              )
            )
          )
        )
      )
    );
    is-deeply $deparsed, 'sub () returns Int { return($x) }', 'deparse';

    for 'AST', EVAL($ast), 'Str', EVAL($deparsed) -> $type, $sub {
        $x = 42;
        lives-ok { $sub() }, "$type: type matches";
        $x = 'oops';
        dies-ok { $sub() }, "$type: type does not match";
    }
}

subtest 'Using a string literal works' => {
    # sub ("Bee") { 42 }
    ast RakuAST::Statement::Expression.new(
      expression => RakuAST::Sub.new(
        signature => RakuAST::Signature.new(
          parameters => (
            RakuAST::Parameter.new(value => "Bee"),
          )
        ),
        body => RakuAST::Blockoid.new(
          RakuAST::StatementList.new(
            RakuAST::Statement::Expression.new(
              expression => RakuAST::IntLiteral.new(42)
            )
          )
        )
      )
    );
    is-deeply $deparsed, 'sub ("Bee") { 42 }', 'deparse';

    for 'AST', EVAL($ast), 'Str', EVAL($deparsed) -> $type, $sub {
        lives-ok { $sub("Bee") }, "$type: type matches";
        dies-ok  { $sub("Boo") }, "$type: type does not match";
    }
}

# vim: expandtab shiftwidth=4
